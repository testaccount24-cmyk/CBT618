         TITLE 'CREACION Y ESCRITURA DE CLAVE EN EL PKDS PARA FIRMA'
*----------------------------------------------------------------------
         PRINT NOGEN
         YREGS
RSADSS1  CSECT
         STM   14,12,12(13)
         BALR  10,0
         USING *,10,11,12
         CNOP  0,4
         B     *+8
         DC    A(SAVEAR)
         L     15,*-4
         ST    13,4(15)
         ST    15,8(13)
         LR    13,15
*----------------------------------------------------------------------
* CALL PKA KEY TOKEN BUILD
*----------------------------------------------------------------------
BUILD    CALL  CSNDPKB,(RETCD,RESCD,EXDATAL,EXDATA,RULECN,             *
               RULEARR,KEYVSL,KEYVSEST,KEYPVLE,LABEL,FIL1,FIL1S,       *
               FIL2,FIL2S,FIL3,FIL3S,FIL4,FIL4S,FIL5,FIL5S,            *
               KEYTOKL,KEYTOKEN)
         CLC   RETCD,=F'0'
         BNE   ERROR
         MVC   WTO1+10(100),KEYTOKEN
WTO1     WTO   '->                                                     *
                                                              <-',     *
               ROUTCDE=11
         MVC   WTO2+10(100),KEYTOKL
WTO2     WTO   '->                                                     *
                                                              <-',     *
               ROUTCDE=11
         PRINT GEN
*----------------------------------------------------------------------
* IMPORT CLEAR RSA PRIVATE EXTERNAL KEY
*----------------------------------------------------------------------
IMPORT   CALL  CSNDPKI,(RETCD,RESCD,EXDATAL,EXDATA,RULEICN,            *
               RULEIAR,KEYTOKL,KEYTOKEN,DUMY_KEK,                      *
               INTPKAL,INTPKA)
         CLC   RETCD,=F'0'
         BNE   ERROR
         WTO   '->IMPORTED<-',ROUTCDE=11
*--------------------------------------------------------------------
*  RECORD CREATE
*--------------------------------------------------------------------
CREATE   CALL  CSNDKRC,(RETCD,RESCD,EXDATAL2,EXDATA2,                  *
               RULEIC,RULEI,LABEL,INTPKAL,INTPKA)
         CLC   RETCD,=F'0'
         BNE   ERROR
         WTO   '->CREATED<-',ROUTCDE=11
*----------------------------------------------------------------------
FIN      L     13,4(13)
         RETURN (14,12),RC=0
ERROR    EQU   *
         L     R2,RETCD
         L     R3,RESCD
         ABEND (R2),REASON=(R3),DUMP
*----------------------------------------------------------------------
SAVEAR   DS    18F            SAVEAREA
RETCD    DS    F
RESCD    DS    F
EXDATAL  DC    F'10'
EXDATA   DC    CL8'        ',X'0000'
EXDATAL2 DC    F'0'
EXDATA2  DS    0C
*---CSNDPKB------------------------------------------------------------
RULEARR  DC    CL16'RSA-PRIVKEY-MGMT'
RULECN   DC    F'2'
*RULEARR  DC    CL8'RSA-PRIV'
*RULECN   DC    F'1'
KEYVSL   DC    F'139'
KEYVSEST DS    0CL139
         DC    X'0200' MODULUS LENGTH IN BITS                X'200'=512
         DC    X'0040' MODULUS FIELD LENGTH IN BYTES  XXX -> X'040'=64
         DC    X'0003' PUB EXP FIELD LENGTH IN BYTES  YYY -> X'003'=3
         DC    X'0040' PRIV EXP FIELD LENGTH IN BYTES ZZZ -> X'040'=64
         DC    X'800000000000000001AE28DA4606D885' \
         DC    X'EB7E0340D6BAAC51991C0CD0EAE835AF' | 008 XXX=64
         DC    X'D9CFF3CD7E7EA74141DADD24A6331BED' | MODULUS N
         DC    X'F41A6626522CCF15767D167D01A16F97' /
         DC    X'010001'     RSA PUBLIC EXP          008+XXX YYY=3
         DC    X'0252BDAD4252BDAD425A8C6045D41AFA' \
         DC    X'F746BEBD5F085D574FCD9C07F0B38C2C' | 008+XXX+YYY
         DC    X'45017C2A1AB919ED2551350A76606BFA' |      ZZZ=64
         DC    X'6AF2F1609A00A0A48DD719A55E9CA801' / RSA SECRET EXP D
KEYPVLE  DC    F'64'
FIL1     DC    F'0'
FIL1S    DC    C' '
FIL2     DC    F'0'
FIL2S    DC    C' '
FIL3     DC    F'0'
FIL3S    DC    C' '
FIL4     DC    F'0'
FIL4S    DC    C' '
FIL5     DC    F'0'
FIL5S    DC    C' '
*---CSNDPKB CSNDKRC----------------------------------------------------
KEYTOKEN DC    1024X'00'
KEYTOKL  DC    F'1024'
LABEL    DC    CL64'RSADSS'
*---CSNDPKI------------------------------------------------------------
RULEIAR  DC    CL8' '
RULEICN  DC    F'0'
DUMY_KEK DS    CL64
INTPKAL  DC    F'1024'
INTPKA   DS    CL1024
*---CSNDKRC------------------------------------------------------------
RULEIC    DC   F'0'
RULEI     DC   C' '
*----------------------------------------------------------------------
         LTORG
         END
