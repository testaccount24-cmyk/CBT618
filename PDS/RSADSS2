         TITLE 'DSS Y VERIFICACION'
*----------------------------------------------------------------------
         PRINT NOGEN
         YREGS
RSADSS2  CSECT
         STM   14,12,12(13)
         BALR  10,0
         USING *,10,11,12
         CNOP  0,4
         B     *+8
         DC    A(SAVEAR)
         L     15,*-4
         ST    13,4(15)
         ST    15,8(13)
         LR    13,15
*----------------------------------------------------------------------
*  CALL ONE-WAY HASH TO GET SHA-1
*----------------------------------------------------------------------
         CALL  CSNBOWH,(RETCD,RESCD,EXDATAL,EXDATA,                    *
               RULEACO,RULEAO,                                         *
               TEXTL,TEXT,                                             *
               CVL,CV,HASHL,HASH)
         CLC   RETCD,=F'0'
         BNE   ERROR
         MVC   WT1+14(L'HASH),HASH
WT1      WTO   'HASH->                    <-',ROUTCDE=11
*----------------------------------------------------------------------
* DIGITAL SIGNATURE GENERATE
*----------------------------------------------------------------------
DSSG     CALL  CSNDDSG,(RETCD,RESCD,EXDATAL,EXDATA,RULEDCN,            *
               RULEDAR,INTPKAL,INTPKA,HASHL,HASH,SIGL,SIGBL,SIG)
         CLC   RETCD,=F'0'
         BNE   ERROR
         MVC   WTO5+10(100),SIG
WTO5     WTO   'KT ->                                                  *
                                                                 <-',  *
               ROUTCDE=11
*----------------------------------------------------------------------
* DIGITAL SIGNATURE VERIFY
*----------------------------------------------------------------------
DSSV     CALL  CSNDDSV,(RETCD,RESCD,EXDATAL,EXDATA,RULEDCN,            *
               RULEDAR,INTPKAL,INTPKA,HASHL,HASH,SIGL,SIG)
         CLC   RETCD,=F'0'
         BNE   ERROR
         WTO   'VERIFY -> CORRECT <-',ROUTCDE=11
*----------------------------------------------------------------------
FIN      L     13,4(13)
         RETURN (14,12),RC=0
ERROR    EQU   *
         L     R2,RETCD
         L     R3,RESCD
         ABEND (R2),REASON=(R3),DUMP
*----------------------------------------------------------------------
SAVEAR   DS    18F            SAVEAREA
RETCD    DS    F
RESCD    DS    F
EXDATAL  DC    F'10'
EXDATA   DC    CL8'        ',X'0000'
*---CSNBOWH------------------------------------------------------------
RULEACO  DC    F'2'
RULEAO   DC    CL16'SHA-1   ONLY    '
TEXTL    DC    F'64'
TEXT     DC    CL64'there''s only 10 types of people in the world; thos*
               e that understand binary and those that don''t '
CVL      DC    F'128'
CV       DS    CL128
HASHL    DC    F'20'
HASH     DS    CL20
*---CSNDDSG------------------------------------------------------------
INTPKAL  DC    F'64'
INTPKA   DC    CL64'RSADSS'
RULEDAR  DC    CL8'ISO-9796'
RULEDCN  DC    F'1'
SIGL     DC    F'128'
SIGBL    DS    F'0'
SIG      DS    CL128
*----------------------------------------------------------------------
         LTORG
         END
